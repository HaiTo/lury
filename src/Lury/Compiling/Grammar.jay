%{

//
// Grammar.jay / Parser.cs
//
// Author:
//       Tomona Nanase <nanase@users.noreply.github.com>
//
// The MIT License (MIT)
//
// Copyright (c) 2014-2015 Tomona Nanase
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.

using System;
using System.Collections.Generic;
using Lury.Resources;
using Lury.Objects;

namespace Lury.Compiling
{
    partial class Parser
    {
        private int yacc_verbose_flag = 0;

%}

/* Whitespace */
%token NewLine Indent Dedent EndOfFile

/* ContextualKeyword */
%token IdentifierGet IdentifierSet IdentifierFile IdentifierLine IdentifierExit
%token IdentifierSuccess IdentifierFailure

/* Keyword */
%token KeywordAbstract KeywordAnd KeywordBreak KeywordCase KeywordCatch KeywordClass
%token KeywordContinue KeywordDef KeywordDefault KeywordDelete KeywordElif KeywordElse
%token KeywordEnum KeywordExtended KeywordFalse KeywordFinally KeywordFor KeywordIf
%token KeywordImport KeywordIn KeywordInterface KeywordInvariant KeywordIs KeywordLazy
%token KeywordNameof KeywordNew KeywordNil KeywordNot KeywordOr KeywordOut KeywordOverride
%token KeywordPass KeywordPrivate KeywordProperty KeywordProtected KeywordPublic
%token KeywordRef KeywordReflect KeywordReturn KeywordScope KeywordSealed KeywordStatic
%token KeywordSuper KeywordSwitch KeywordThis KeywordThrow KeywordTrue KeywordTry
%token KeywordUnittest KeywordUnless KeywordUntil KeywordVar KeywordWhile KeywordWith
%token KeywordYield

/* Identifier */
%token Identifier

/* Literal */
%token StringLiteral EmbedStringLiteral WysiwygStringLiteral
%token ImaginaryNumber FloatNumber Integer

/* Operator */
%token RangeOpen RangeClose Increment AssignmentAdd Decrement AssignmentSub
%token AnnotationReturn AssignmentConcat AssignmentPower Power AssignmentMultiply
%token AssignmentIntDivide IntDivide AssignmentDivide AssignmentModulo AssignmentLeftShift
%token LeftShift LessThan AssignmentRightShift RightShift MoreThan Equal Lambda NotEqual
%token NotIn IsNot AndShort AssignmentAnd AssignmentXor OrShort AssignmentOr NilCoalesce

//%type <int> Expression

%left  ','
%right '=' AssignmentPower AssignmentMultiply AssignmentDivide AssignmentIntDivide AssignmentModulo AssignmentAdd AssignmentSub AssignmentConcat AssignmentOr AssignmentAnd AssignmentXor AssignmentLeftShift AssignmentRightShift
%right '?' ':' KeywordIf KeywordElse
%left  OrShort KeywordOr
%left  AndShort KeywordAnd
%right '!' KeywordNot
%left  Equal NotEqual '<' '>' LessThan MoreThan KeywordIs IsNot
%left  '|'
%left  '^'
%left  '&'
%left  LeftShift RightShift
%left  '+' '-' '~'
%left  '*' IntDivide '/' '%'
%right Power
%right Increment Decrement
%left  '.'

%start program

%%

program
    :
        {
            $$ = new Routine();
        }
    | program_lines
        {
            $$ = new Routine((IEnumerable<Statement>)$1);
        }

program_lines
    : program_line
        {
            var list = new List<Statement>();

            if ($1 != null)
            	list.Add((Statement)$1);

           	$$ = list;
        }
    | program_lines program_line
        {
        	if ($2 == null)
        	{
        		$$ = $1;
        	}
        	else
        	{
	            var list = (List<Statement>)$1;
	            var newList = new List<Statement>(list);
	            newList.Add((Statement)$2);
	            $$ = newList;
            }
        }


program_line
    : statement
    | NewLine
    	{
    		$$ = null;
    	}


statement
    : statement_list NewLine
        {
            $$ = $1;
        }
    | compound_statement

statement_list
    : simple_statement
    | simple_statement simple_statements
        {
            throw new NotImplementedException();
        }

simple_statements
    : ';'
    | ';' simple_statement simple_statements

compound_statement
    : if_statement
    | while_statement
    | function_definition
        {
            throw new NotImplementedException();
        }
    | class_definition
        {
            throw new NotImplementedException();
        }

suite
    : statement_list NewLine
        {
            throw new NotImplementedException();
        }
    | NewLine Indent statements Dedent
        {
            $$ = new Routine((IEnumerable<Statement>)$3);
        }

statements
    : statement
        {
            $$ = new List<Statement>() { (Statement)$1 };
        }
    | statements statement
        {
            var list = (List<Statement>)$1;
            var newList = new List<Statement>(list);
            newList.Add((Statement)$2);
            $$ = newList;
        }

if_statement
    : KeywordIf expression ':' suite
        {
            $$ = new IfStatement((Node)$2, (Routine)$4);
        }
    | KeywordIf expression ':' suite elif_statements
        {
            $$ = new IfStatement((Node)$2, (Routine)$4, (IfStatement)$5);
        }

elif_statements
    : KeywordElif expression ':' suite
        {
            $$ = new IfStatement((Node)$2, (Routine)$4);
        }
    | KeywordElif expression ':' suite elif_statements
        {
            $$ = new IfStatement((Node)$2, (Routine)$4, (IfStatement)$5);
        }
    | else_statement
        {
            $$ = new IfStatement((Routine)$1);
        }

else_statement
    : KeywordElse ':' suite
        {
            $$ = $3;
        }

while_statement
    : KeywordWhile expression ':' suite
        {
            $$ = new WhileStatement((Node)$2, (Routine)$4);
        }
    | KeywordWhile expression ':' suite else_statement
        {
            throw new NotImplementedException();
        }

function_definition
    : KeywordDef function_name '(' parameter_list ')' ':' suite
    | KeywordDef function_name ':' suite

function_name
    : identifier_all
    | function_name '.' identifier_all

parameter_list
    : parameter
    | parameter_list ',' parameter

parameter
    : identifier_all

class_definition
    : KeywordClass class_name '(' inheritance_list ')' ':' suite
    | KeywordClass class_name ':' suite

class_name
    : identifier_all

inheritance_list
    : object_name
    | inheritance_list ',' object_name

object_name
    : identifier_all
    | object_name '.' identifier_all


simple_statement
    : pass_statement
        {
            throw new NotImplementedException();
        }
    | expression_statement
    | return_statement
        {
            throw new NotImplementedException();
        }
    | break_statement
        {
            throw new NotImplementedException();
        }
    | continue_statement
        {
            throw new NotImplementedException();
        }
    | import_statement
        {
            throw new NotImplementedException();
        }

pass_statement
    : KeywordPass

expression_statement
    : expression
    	{
    		$$ = new ExpressionStatement((Node)$1);
    	}

return_statement
    : KeywordReturn
    | KeywordReturn expression

break_statement
    : KeywordBreak

continue_statement
    : KeywordContinue

import_statement
    : KeywordImport import_names
    | KeywordPublic KeywordImport import_names

import_names
    : import_name
    | import_names ',' import_name

import_name
    : module_name

module_name
    : identifier_all
    | module_name '.' identifier_all

identifier_all
    : Identifier
    | contextual_keyword

contextual_keyword
    : IdentifierGet
    | IdentifierSet
    | IdentifierFile
    | IdentifierLine
    | IdentifierExit
    | IdentifierSuccess
    | IdentifierFailure


expression
    : comma_expression

comma_expression
    : assignment_expression
    | assignment_expression ',' comma_expression
        {
            throw new NotImplementedException();
        }

assignment_expression 
    : conditional_expression
    | conditional_expression '=' assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.Assign);
        }
    | conditional_expression AssignmentAdd assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.Addition);
        }
    | conditional_expression AssignmentSub assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.Subtraction);
        }
    | conditional_expression AssignmentMultiply assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.Multiplication);
        }
    | conditional_expression AssignmentDivide assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.Division);
        }
    | conditional_expression AssignmentIntDivide assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.IntDivision);
        }
    | conditional_expression AssignmentPower assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.Power);
        }
    | conditional_expression AssignmentModulo assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.Modulo);
        }
    | conditional_expression AssignmentAnd assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.ArithmeticAnd);
        }
    | conditional_expression AssignmentOr assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.ArithmeticOr);
        }
    | conditional_expression AssignmentXor assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.ArithmeticXor);
        }
    | conditional_expression AssignmentConcat assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.Concatenation);
        }
    | conditional_expression AssignmentLeftShift assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.LeftShift);
        }
    | conditional_expression AssignmentRightShift assignment_expression
        {
            $$ = new BinaryAssignNode((Node)$1, (Node)$3, BinaryAssignOperator.RightShift);
        }

conditional_expression
    : bool_or_expression
    | bool_or_expression '?' bool_or_expression ':' bool_or_expression
        {
            throw new NotImplementedException();
        }
    | bool_or_expression KeywordIf bool_or_expression KeywordElse bool_or_expression
        {
            throw new NotImplementedException();
        }

bool_or_expression
    : bool_and_expression
    | bool_or_expression KeywordOr bool_and_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.LogicalOr);
        }
    | bool_or_expression OrShort bool_and_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.LogicalOr);
        }

bool_and_expression
    : bool_not_expression
    | bool_and_expression KeywordAnd bool_not_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.LogicalAnd);
        }
    | bool_and_expression AndShort bool_not_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.LogicalAnd);
        }

bool_not_expression
    : comparison_expression
    | KeywordNot bool_not_expression
        {
            $$ = new UnaryNode((Node)$2, UnaryOperator.LogicalNot);
        }
    | '!' bool_not_expression
        {
            $$ = new UnaryNode((Node)$2, UnaryOperator.LogicalNot);
        }

comparison_expression
    : or_expression
    | or_expression comparison_operator or_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, (BinaryOperator)$2);
        }

comparison_operator
    : '<'
        {
            $$ = BinaryOperator.LessThan;
        }
    | '>'
        {
            $$ = BinaryOperator.GreaterThan;
        }
    | LessThan
        {
            $$ = BinaryOperator.LessThanEqual;
        }
    | MoreThan
        {
            $$ = BinaryOperator.GreaterThanEqual;
        }
    | Equal
        {
            $$ = BinaryOperator.Equal;
        }
    | NotEqual
        {
            $$ = BinaryOperator.NotEqual;
        }
    | KeywordIs
        {
            $$ = BinaryOperator.Is;
        }
    | IsNot
        {
            $$ = BinaryOperator.IsNot;
        }
    | KeywordIs KeywordNot
        {
            $$ = BinaryOperator.IsNot;
        }

or_expression
    : xor_expression
    | or_expression '|' xor_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.ArithmeticOr);
        }

xor_expression
    : and_expression
    | xor_expression '^' and_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.ArithmeticXor);
        }

and_expression
    : shift_expression
    | and_expression '&' shift_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.ArithmeticAnd);
        }

shift_expression
    : addition_expression
    | shift_expression LeftShift addition_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.LeftShift);
        }
    | shift_expression RightShift addition_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.RightShift);
        }

addition_expression
    : multiplication_expression
    | addition_expression '+' multiplication_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.Addition);
        }
    | addition_expression '-' multiplication_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.Subtraction);
        }
    | addition_expression '~' multiplication_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.Concatenation);
        }

multiplication_expression 
    : unary_expression
    | multiplication_expression '*' unary_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.Multiplication);
        }
    | multiplication_expression IntDivide unary_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.IntDivision);
        }
    | multiplication_expression '/' unary_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.Division);
        }
    | multiplication_expression '%' unary_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.Modulo);
        }

unary_expression
    : power_expression
    | Increment unary_expression
        {
            $$ = new UnaryAssignNode((Node)$2, UnaryAssignOperator.IncrementPrefix);
        }
    | Decrement unary_expression
        {
            $$ = new UnaryAssignNode((Node)$2, UnaryAssignOperator.DecrementPrefix);
        }
    | '-' unary_expression
        {
            $$ = new UnaryNode((Node)$2, UnaryOperator.SignNegative);
        }
    | '+' unary_expression
        {
            $$ = new UnaryNode((Node)$2, UnaryOperator.SignPositive);
        }
    | '~' unary_expression
        {
            $$ = new UnaryNode((Node)$2, UnaryOperator.BitwiseNot);
        }

power_expression
    : postfix_expression
    | power_expression Power postfix_expression
        {
            $$ = new BinaryNode((Node)$1, (Node)$3, BinaryOperator.Power);
        }

postfix_expression
    : primary
    | postfix_expression '.' identifier_all
        {
            throw new NotImplementedException();
        }
    | postfix_expression Increment
        {
            $$ = new UnaryAssignNode((Node)$1, UnaryAssignOperator.IncrementPostfix);
        }
    | postfix_expression Decrement
        {
            $$ = new UnaryAssignNode((Node)$1, UnaryAssignOperator.DecrementPostfix);
        }
    | postfix_expression '(' ')'
        {
            $$ = new CallNode((Node)$1);
        }
    | postfix_expression '(' argument_list ')'
        {
            $$ = new CallNode((Node)$1, (IEnumerable<Node>)$3);
        }

argument_list
    : argument
        {
            $$ = new List<Node>() { (Node)$1 };
        }
    | argument_list ',' argument
        {
            var list = (List<Node>)$1;
            var newList = new List<Node>(list);
            newList.Add((Node)$3);
            $$ = newList;
        }

argument
    : assignment_expression
    | named_argument

named_argument
    : identifier_all ':' expression
        {
            throw new NotImplementedException();
        }

primary
    : identifier_all
        {
            $$ = new LValueNode($1);
        }
    | literal
    | KeywordTrue
        {
            $$ = new ConstantNode(this.CreateBoolean(true));
        }
    | KeywordFalse
        {
            $$ = new ConstantNode(this.CreateBoolean(false));
        }
    | KeywordNil
        {
            $$ = new ConstantNode(null);
        }
    | KeywordThis
        {
            $$ = new LValueNode("this");
        }
    | KeywordSuper
        {
            $$ = new LValueNode("super");
        }
    | '(' expression ')'
        {
            $$ = $2;
        }

literal
    : StringLiteral
        {
            $$ = new ConstantNode(this.CreateString($1, '\''));
        }
    | EmbedStringLiteral
        {
            $$ = new ConstantNode(this.CreateString($1, '"'));
        }
    | WysiwygStringLiteral
        {
            $$ = new ConstantNode(this.CreateString($1, '`'));
        }
    | ImaginaryNumber
        {
            $$ = new ConstantNode(this.CreateComplex($1));
        }
    | FloatNumber
        {
            $$ = new ConstantNode(this.CreateReal($1));
        }
    | Integer
        {
            $$ = new ConstantNode(this.CreateInteger($1));
        }
    
%%

    }
